plugins {
    id 'java'
}

group 'org.example'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

configurations {
    testImplementation {
        canBeResolved=true
    }
}

dependencies {
    testImplementation 'org.assertj:assertj-core:3.22.0'
    testImplementation 'com.codeborne:selenide:6.4.0'
    testImplementation 'org.jbehave:jbehave-core:4.8.3'
    testImplementation 'org.jbehave.site:jbehave-site-resources:3.4.1@zip'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.2'
}

def jarPath(String jarName) {
    configurations.testImplementation.filter{it.name.startsWith(jarName)}.asPath
}

task testJBehave(type: Test) {
    include '**/JBehaveTest*'
}

test {
    doFirst {
        copy {
            from(zipTree(jarPath("jbehave-core"))) {
                include "style/*"
            }
            into("build/reports/jbehave/view")

        }
        copy {
            from(zipTree(jarPath("jbehave-site-resources"))) {
                include "js/**/*"
                include "style/**/*"
                include "images/*"
            }
            into("build/reports/jbehave/view")
        }
    }
    useJUnitPlatform()
}